plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'com.github.ghubstan'
version '1.0.0'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

java {
    modularity.inferModulePath = true
}

tasks.named('compileJava') {
    sourceCompatibility = 15
    modularity.inferModulePath = true
    options.javaModuleVersion = provider { project.version }
    options.compilerArgs += ['-Xdoclint:none', '-Xlint:none', '-nowarn']
}

tasks.named("test") {
    useJUnitPlatform()
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

ext { // in alphabetical order
    guavaVersion = '30.1.1-jre'
    jacksonVersion = '2.12.1'
    javaxAnnotationVersion = '1.3.2'
    jetbrainsAnnotationsVersion = '13.0'
    jsonrpc4jVersion = '1.6.0.bisq.1'
    jupiterVersion = '5.7.1'
    logbackVersion = '1.3.0-alpha5'  // force to upgrade to modularized logback
    lombokVersion = '1.18.20'
    slf4jVersion = '2.0.0-alpha1'   // force to upgrade to modularized logback
}

dependencies {
    // Note:  Double quotes are required when using groovyâ€™s string interpolation.
    implementation "com.github.jitpack:gradle-simple:1.1"

    implementation "com.google.errorprone:error_prone_annotations:2.5.1"
    implementation ("com.google.guava:guava:$guavaVersion") {
        exclude(module: 'failureaccess')
        exclude(module: 'findbugs')
        exclude(module: 'listenablefuture')
        exclude(module: 'jsr305')
        exclude(module: 'j2objc-annotations')
    }

    implementation "org.checkerframework:checker-qual:3.13.0"
    implementation "org.slf4j:slf4j-api:$slf4jVersion"
    implementation ("ch.qos.logback:logback-core:$logbackVersion")  {
        exclude(module: 'checker-framework')
    }
    implementation ("ch.qos.logback:logback-classic:$logbackVersion") {
        exclude(module: 'checker-framework')
        exclude(module: 'javax.mail')
    }

    compileOnly "javax.annotation:javax.annotation-api:$javaxAnnotationVersion"
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$jupiterVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$jupiterVersion"
}

test {
    useJUnitPlatform()
}

publishing {
    // $ ./gradlew clean build assemble publish
    // Create pom.xml -> ./gradlew clean build publishToMavenLocal
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/ghubstan/jitpackpubdemo")
            credentials {
                username = "ghubstan"
                password =  "[ghub-token]"
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = 'com.github.ghubstan'
            artifactId = 'jitpackpubdemo'
            version = '1.0.0'

            from components.java
        }
    }
}
